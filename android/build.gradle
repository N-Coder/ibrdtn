buildscript {
    ext.kotlin_version = '1.3.41'

    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


allprojects {
    version = "1.2.0"

    def commit = System.getenv().containsKey("GIT_COMMIT")
    if (commit) {
        version  = version  + "-" + commit
    }

    ext {
        versionCode = 1200
    }

    group = "de.tubs.ibr.dtn"

    repositories {
        jcenter()
        google()
    }
}

def defaultAndroidConfig(project) {
    project.android {
        compileSdkVersion 28

        defaultConfig {
            minSdkVersion 24 // see app/src/main/jni/Application.mk#APP_PLATFORM
            targetSdkVersion 28
            versionCode project.ext.versionCode
            versionName project.version
        }
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE") ?: "publish.keystore")
                storePassword System.getenv("KEYSTORE_PASSWD")
                keyAlias System.getenv("KEY_ALIAS") ?: project.android.defaultConfig.applicationId
                keyPassword System.getenv("KEY_PASSWD")
            }
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    project.dependencies {
        implementation 'com.android.support:support-compat:28.0.0'
        implementation 'com.android.support:support-fragment:28.0.0'
    }
}
